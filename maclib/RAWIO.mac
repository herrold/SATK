.* Copyright (C) 2017 Harold Grovesteen
.*
.* This file is part of SATK.
.*
.*     SATK is free software: you can redistribute it and/or modify
.*     it under the terms of the GNU General Public License as published by
.*     the Free Software Foundation, either version 3 of the License, or
.*     (at your option) any later version.
.*
.*     SATK is distributed in the hope that it will be useful,
.*     but WITHOUT ANY WARRANTY; without even the implied warranty of
.*     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
.*     GNU General Public License for more details.
.*
.*     You should have received a copy of the GNU General Public License
.*     along with SATK.  If not, see <http://www.gnu.org/licenses/>.

         MACRO
&LABEL   RAWIO  &REG,&FAIL=,&CERR=,&UERR=,&SENSE=,&EOF=,&IOSAVE='YES',&CHAN=X'FE'
.* Performs a low-level in-line I/O operation using a control block based upon the
.* current I/O architecture as implied by the assembly time architecture level.
.*
.* Assembly Requirements:
.* The macro requires an established base register and addressability to the control
.* block using the IOCB DSECT and the area defined by ASAREA.
.*
.* Run-time Requirements:
.* General register 1 contains the device identifier:
.*   - a channel/unit address for channel based I/O or
.*   - a subchannel subsystem ID for subchannel-based operations.
.*
.* Required Parameter Usage:
.*   &FAIL   Control passes here for a failure to initiate the operation (required).
.*   &REG    Channel subsystem structure addressing.  Required for subchannel
.*           operations.  Ignored if provided for channel-based operations.
.* Optional Parameter Usage:
.*   &LABLE  Label assigned to start of in-line logic.
.*   &CERR   Control passes here for a detetected channel error. If ommitted channel
.*           errors are not checked.
.*   &UERR   Control passes here for a detetected unit error. If ommitted unit
.*           errors are not checked.
.*   &SENSE  Optionally passes control to this label if unit check reported in status.  If ommitted
.*           Note, if not handled separately unit check should be detected by the
.*           &UERR parameter.
.*   &EOF    Optionally passes control to this label if after I/O completed a
.*           physical end-of-file condition reported by a unit exception status.
.*   &IOSAVE IOSAVE parameter for CPUWAIT macro.  See CPUWAIT for usage.  Defaults
.*           to 'YES'.
.*   &CHAN   PSW channel interruption mask.  Ignored for channel subystem or
.*           channel I/O system not operating as a System/360 or System/370 in
.*           BC-mode.  Defaults to X'FE'.
.* Note: System/370 initializes CR2 to all ones, thereby automatically enabling all
.* channels for input/output operations.  No explicit system initialization is
.* therefore required for use.
.*
.* Depends upon:
.*   ARCHLVL macro ininitialized &ARCHLVL symbolic variable
.*   ARCHIND macro in satk.mac for dynamic instruction selection
.*   ASAREA macro in satk.mac
.*   CPUWAIT macro in satk.mac
         GBLA  &ARCHLVL Current architecture level
.* Symbols supplied by ARCHIND macro:
.*         GBLA  &ARCHASZ   Size of an address constant in bytes
.*         GBLA  &ARCHIRB   Size of an IRB in full words
.*         GBLA  &ARCHORB   Size of an ORB in full words
.*         GBLA  &ARCHORX   Size of an extended ORB in full words
.*         GBLA  &ARCHSCB   Size of a SCHIB in full words
         GBLB  &ARCHIND   Whether operator synonyms defined
.*       GBLC  &ARCHATP   Address constant type
.*       GBLC  &ARCHITP   Integer constant type
.* Symbol supplied by IOCBDS macro
         GBLB  &SATKIOB Whether the control block DSECT has been generated
         LCLB  &BC    Whether basic control mode in use
         LCLB  &CS    Whether channel subsystem in use
&BC      SETB  &ARCHLVL EQ 1 OR &ARCHLVL EQ 2
&CS      SETB  &ARCHLVL GE 5
         AIF   ('&REG' NE '' OR &ARCHLVL LT 5).DEVGOOD
         MNOTE 1,'RAWIO - REQUIRED REG PARAMETER MISSING'
         MEXIT
.DEVGOOD ANOP
         AIF   ('&FAIL' NE '').FALGOOD
         MNOTE 1,'RAWIO - REQUIRED FAIL PARAMETER MISSING'
         MEXIT
.FALGOOD ANOP
         AIF   (&ARCHIND).GEN
         MNOTE 1,'RAWIO - REQUIRED ARCHIND MACRO OPERATOR SYNONYMS UNDEFINED'
         MEXIT
.GEN     ANOP
         AIF   ('&LABEL' EQ '').NOLBL
&LABEL   DS    0H
.NOLBL   ANOP
         AIF   (NOT &CS).NOSCCLR
         MVI   IOCBSC,X'00'          Clear SC information
.NOSCCLR ANOP
         MVC   IOCBST,IOCBZERO       Clear accumulated status
         L     1,IOCBDID             Remember the device ID with which I am working
         AIF   (&CS).DOCSIO
* Initiate Channel-based input/output operation
         MVC   CAW,IOCBCAW           Establish CAW
         SIO   0(1)                  Initiate the I/O operation
         BC    B'0111',&FAIL         ..Failed to start, report/handle the error
         AGO   .WAIT
.DOCSIO  ANOP
* Initiate Subchannel-based input/output operation
         $L    &REG,IOCBORB          Locate the ORB for the channel subsystem
         SSCH  0(&REG)               Initiate the I/O operation
         $BC   B'0111',&FAIL         ..Start function failed, report/handle the error
         $L    &REG,IOCBIRB          Locate the IRB storage area
         USING IRB,&REG              Make it addressable
.WAIT    ANOP
         SPACE 1
* Wait for I/O operation to present status via an interruption
IOWT&SYSNDX DS 0H  Wait for I/O to complete
.*         CPUWAIT IO=YES,IOSAVE=&IOSAVE,CHAN=&CHAN
         CPUWAIT IO=YES,CHAN=&CHAN
         AIF   (&CS).CKIRB
         SPACE 1
* Validate interruption is for the expected device
         AIF   (&BC).TSTBCDV
         CH    1,IOICODE             Is this the device for which I am waiting?
         AGO   .MYIO
.TSTBCDV ANOP
         CH    1,BCIOCOD             Is this the device for which I am waiting?
.MYIO    ANOP
         BNE   IOWT&SYSNDX           ..No, continue waiting for it
* Accumulate interruption information from CSW
         MVC   IOCBSCCW+1(3),CSW+CSWCCW   CCW address
         MVC   IOCBRCNT,CSW+CSWCNT   Residual count
         OC    IOCBST,CSW+CSWUS      ..Yes, accumulate unit and channel status
         AGO   .CHECK
.CKIRB   ANOP
* Validate interruption is for the expected subchannel
         CL    1,IOSSID              Is this the device for which I am waiting?
         $BNE  IOWT&SYSNDX           ..No, continue waiting for it
* Accumulate interruption information from IRB
         TSCH  0(&REG)               Retrive interrupt information
         $BC   B'0100',IOWT&SYSNDX   CC1 (not status pending), wait for it to arrive
         $BC   B'0001',&FAIL         CC3 (not operational), an error then
*                                    CC0 (status was pending), accumulate the status
         OC    IOCBSC,IRBSCSW+SCSW2  Accumulate status control
         OC    IOCBST,IRBSCSW+SCSWUS Accumulate device and channel status
         TM    IOCBSC,SCSWSPRI       Primary subchannel status?
         $BNO  IOWT&SYSNDX           ..No, wait for primary status
         MVC   IOCBSCCW,IRBSCSW+SCSWCCW  CCW address
         MVC   IOCBRCNT,IRBSCSW+SCSWCNT  Residual count
.CHECK   ANOP
* Test for errors as specified in the IOCB
         AIF   ('&CERR' EQ '').NOCERR
         MVC   IOCBCT,IOCBCS         Move channel status for testing
         NC    IOCBCT,IOCBCM         Test for errors
         $BNZ  &CERR                 ..Report/handle the channel error
.NOCERR  ANOP
         AIF   ('&UERR' EQ '').NOUERR
         MVC   IOCBUT,IOCBUS         Move unit status for testing
         NC    IOCBUT,IOCBUM         Test for errors
         $BNZ   &UERR                ..Report/handle the device error
.NOUERR  ANOP
         AIF   ('&SENSE' EQ '').NOSENSE
         TM    IOCBDS,CSWUC          Unit check status?
         $BO   &SENSE                ..Yes, handle the unit check status
.NOSENSE ANOP
         TM    IOCBUS,CSWCE+CSWDE    Channel end and device end both accumulated?
         AIF   (&ARCHLVL GE 5).NOCEDE
         $BNO  IOWT&SYSNDX           ..No, continue waiting for both to show up
         AGO   .CKEOF
.NOCEDE  ANOP
         $BNO  &FAIL                 Hunh? No CE and DE but do have primary status!
.CKEOF   ANOP
         AIF   ('&EOF' EQ '').NOEOF
         TM    IOCBUS,CSWUX          Was physical END-OF-FILE detected?
         $BO   &EOF                  ..Yes, report/handle end-of-file condition
.NOEOF   ANOP
* Input/Output operation successful
.DONE    MEND
