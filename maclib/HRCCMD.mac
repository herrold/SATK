.* Copyright (C) 2017 Harold Grovesteen
.*
.* This file is part of SATK.
.*
.*     SATK is free software: you can redistribute it and/or modify
.*     it under the terms of the GNU General Public License as published by
.*     the Free Software Foundation, either version 3 of the License, or
.*     (at your option) any later version.
.*
.*     SATK is distributed in the hope that it will be useful,
.*     but WITHOUT ANY WARRANTY; without even the implied warranty of
.*     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
.*     GNU General Public License for more details.
.*
.*     You should have received a copy of the GNU General Public License
.*     along with SATK.  If not, see <http://www.gnu.org/licenses/>.

         MACRO
&LABEL   HRCCMD &CMD,&CMDL=,&FAIL=
.* Issue a Hercules command usind DIAGNOSE X'8' without a response buffer.
.* Required Parameter Usage:
.*  &CMD    The register pointing to command to be issued
.* Optional Parameter Usage:
.*  &LABEL  Symbols associated with the macro's generated statements.
.*  &CMDL   The register containing the length of the command.  If omitted, the length
.*          is assumed to be the halfword pointed to by the &CMD register followed
.*          by the actual command as EBCDIC characters.
.*  &FAIL   Control is passed to this location if the command fails.  If omitted,
.*          a disabled wait state with code X'400' is entered.
.* Dependency:
.*  Entering a disabled wait state has a dependency on the DWAIT macro in satk.mac
.* WARNING: A command length of zero will cause the Hercules CPU to enter the stopped
.* state!
         GBLA  &ARCHLVL Current architecture level
         LCLB  &CLIMP   Switch indicating the length is implied.
         LCLC  &CLREG   Command length register
.*         LCLC  &BNZ,&BZ,&LH,&SLR
         AIF   ('&CMD' NE '').CMDOK
         MNOTE 1,'HRCCMD - INITIAL COMMAND LENGTH REGISTER PARAMETER MISSING'
         MEXIT
.CMDOK   ANOP
&CLIMP   SETB  '&CMDL' EQ ''
         AIF   (&CLIMP).IMPCL
&CLREG   SETC  '&CMDL'
         AGO   .CKLBL
.IMPCL   ANOP
&CLREG   SETC  '2+(&CMD)'
.CKLBL   ANOP
         AIF   ('&LABEL' EQ '').NOLBL
&LABEL   DS    0H
.NOLBL   ANOP
         AIF   (NOT &CLIMP).DODIAG
         $SLR  &CLREG,&CLREG             Make sure flags are all zeros
         ICM   &CLREG,B'0011',0(&CMD)    Fetch the command length
         LA    &CMD,2(0,&CMD)            Point to the actual command
.DODIAG  ANOP
.* A Specification exception results if the registers are bad or flags are invalid
.* A command length of zero results in a stopped CPU on Hercules.  This macro
.* does not check for a zero length command.
         DIAG  &CMD,&CLREG,X'008'        Issue the Hercules command
         AIF   ('&FAIL' EQ '').DWAIT
         $BNZ  &FAIL
         MEXIT
.DWAIT   ANOP
         $BZ    HRCC&SYSNDX
         DWAIT PGM=0,CODE=400,LOAD=YES
HRCC&SYSNDX DS 0H
         MEND
